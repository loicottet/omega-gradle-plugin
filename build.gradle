plugins {
    id 'java-gradle-plugin'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'maven-publish'
}

group 'com.gluonhq'
version '0.0.5-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "https://nexus.gluonhq.com/nexus/content/repositories/releases"
    }
    mavenLocal()
}

dependencies {
    implementation gradleApi()

    implementation 'com.gluonhq:omega:0.0.5-SNAPSHOT'
}

gradlePlugin {
    plugins {
        javafxPlugin {
            id = 'com.gluonhq.omegaplugin'
            displayName = 'JavaFX Omega Plugin'
            description = 'Plugin that simplifies using Gluon Omega for JavaFX projects'
            implementationClass = 'com.gluonhq.gradle.OmegaPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gluonhq/omega-gradle-plugin'
    vcsUrl = 'https://github.com/gluonhq/omega-gradle-plugin'
    tags = [ 'java', 'javafx', 'gluon', 'omega' ]
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'omega-gradle-plugin'
            version = project.version
        }
    }
    repositories {
        maven {
           def repositoryUrl = project.hasProperty('repositoryUrl') ? project.getProperty('repositoryUrl') : 'https://nexus.gluonhq.com/nexus/content/repositories/snapshots/'
           def repositoryUsername = project.hasProperty('repositoryUsername') ? project.getProperty('repositoryUsername') :  project.getProperty('gluonNexusUsername')
           def repositoryPassword = project.hasProperty('repositoryPassword') ? project.getProperty('repositoryPassword') :  project.getProperty('gluonNexusPassword')

           url repositoryUrl
           credentials {
              username repositoryUsername
              password repositoryPassword
          }
      }
  }
}

license {
    skipExistingHeaders = true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
